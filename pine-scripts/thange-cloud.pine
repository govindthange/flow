// Â© 2021
// @ govindthange

//@version=5
indicator("Thange Cloud Playbook", shorttitle="Thange Signals", overlay=true)

// Collect inputs

atrLength = input.int(30, minval=1, title="ATR Length")
atrStopMultiplier = input.float(1.0, minval=0.1, title="ATR x ?")

conversionPeriods = input.int(9, minval=1, title="Tenkan Sen Length")
basePeriods = input.int(26, minval=1, title="Kijun Sen Length")
senkouSpanBPeriod = input.int(52, minval=1, title="Leading Span B Length")
displacement = input.int(26, minval=1, title="Displacement")
donchian(len) => math.avg(ta.lowest(len), ta.highest(len))

tenkanSen = donchian(conversionPeriods)
kijunSen = donchian(basePeriods)
senkouSpanA = math.avg(tenkanSen, kijunSen)
senkouSpanB = donchian(senkouSpanBPeriod)

// Calculate entries using ATR
r = ta.atr(atrLength) * atrStopMultiplier
longEntry = kijunSen - r
shortEntry = kijunSen + r

// Evaluate for bullish & bearish confluence

hasBullishConfluence = senkouSpanB > senkouSpanA // current Kumo is green
hasBearishConfluence = senkouSpanA > senkouSpanB // current Kumo is red

// Plot LONG or SHORT signals based on confluence
plotshape(hasBullishConfluence, title= "Long Signal", location=location.belowbar, color=color.green, transp=0, style=shape.triangleup, text="")
plotshape(hasBearishConfluence,  title= "Short Signal", location=location.abovebar, color=color.red, transp=0, style=shape.triangledown, text="")

// Create string format template to restrict SL precision to ticks.
f_tickFormat() =>
    _s = str.tostring(syminfo.mintick)
    _s := str.replace_all(_s, "25", "00")
    _s := str.replace_all(_s, "5",  "0")
    _s := str.replace_all(_s, "1",  "0")

// Draw labels to enter or trail a stop-loss order based on confluence

if (hasBullishConfluence)
    var labelLong = label.new(bar_index, kijunSen,  text = "Trail S.L.", style = label.style_label_lower_left, color = color.green)
    label.set_xy(labelLong, bar_index, kijunSen)
    label.set_tooltip(labelLong, "Stop loss @ "  + str.tostring(kijunSen, str.tostring(f_tickFormat())))
else if (hasBearishConfluence)
    var labelShort = label.new(bar_index, kijunSen, text = "Trail S.L.", style = label.style_label_upper_left, color = color.red)
    label.set_xy(labelShort, bar_index, kijunSen)
    label.set_tooltip(labelShort, "Stop loss @ " + str.tostring(kijunSen, str.tostring(f_tickFormat())))

// Draw Table

var table atrDisplay = table.new(position.top_center, 2, 1, bgcolor = color.black, frame_width = 2, frame_color = color.black)
if (barstate.islast)
    table.cell(atrDisplay, 0, 0, str.tostring(longEntry, str.tostring(f_tickFormat())), text_color = color.green)
    table.cell(atrDisplay, 1, 0, str.tostring(shortEntry, str.tostring(f_tickFormat())), text_color = color.red)
