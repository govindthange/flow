// Â© 2021
// @ govindthange

//@version=5
indicator("Thange Cloud Playbook", shorttitle="Thange Signals", overlay=true)

// Collect inputs

atrLength = input.int(30, minval=1, title="ATR length")
atrStopMultiplier = input.float(1.0, minval=0.1, title="ATR x ?")

conversionPeriods = input.int(9, minval=1, title="Conversion Line Length")
basePeriods = input.int(26, minval=1, title="Base Line Length")
laggingSpan2Periods = input.int(52, minval=1, title="Leading Span B Length")
displacement = input.int(26, minval=1, title="Displacement")
donchian(len) => math.avg(ta.lowest(len), ta.highest(len))

conversionLine = donchian(conversionPeriods)
baseLine = donchian(basePeriods)
leadLine1 = math.avg(conversionLine, baseLine)
leadLine2 = donchian(laggingSpan2Periods)

// Calculate ATR
r = ta.atr(atrLength) * atrStopMultiplier


// Calculate entries
longEntry = baseLine - r
shortEntry = baseLine + r

// Evaluate for bullish & bearish confluence

hasBullishConfluence = leadLine2 > leadLine1 // current Kumo is green
hasBearishConfluence = leadLine1 > leadLine2 // current Kumo is red

// Plot LONG or SHORT signals based on confluence
plotshape(hasBullishConfluence, title= "Long Signal", location=location.belowbar, color=color.green, transp=0, style=shape.triangleup, text="")
plotshape(hasBearishConfluence,  title= "Short Signal", location=location.abovebar, color=color.red, transp=0, style=shape.triangledown, text="")

// Create string format template to restrict SL precision to ticks.
f_tickFormat() =>
    _s = str.tostring(syminfo.mintick)
    _s := str.replace_all(_s, "25", "00")
    _s := str.replace_all(_s, "5",  "0")
    _s := str.replace_all(_s, "1",  "0")

// Draw labels to enter or trail a stop-loss order based on confluence

if (hasBullishConfluence)
    var labelLong = label.new(bar_index, baseLine,  text = "Trail S.L.", style = label.style_label_lower_left, color = color.green)
    label.set_xy(labelLong, bar_index, baseLine)
    label.set_tooltip(labelLong, "Stop loss @ "  + str.tostring(baseLine, str.tostring(f_tickFormat())))
else if (hasBearishConfluence)
    var labelShort = label.new(bar_index, baseLine, text = "Trail S.L.", style = label.style_label_upper_left, color = color.red)
    label.set_xy(labelShort, bar_index, baseLine)
    label.set_tooltip(labelShort, "Stop loss @ " + str.tostring(baseLine, str.tostring(f_tickFormat())))

// Draw Table

var table atrDisplay = table.new(position.top_center, 2, 1, bgcolor = color.black, frame_width = 2, frame_color = color.black)
if (barstate.islast)
    table.cell(atrDisplay, 0, 0, str.tostring(longEntry, str.tostring(f_tickFormat())), text_color = color.green)
    table.cell(atrDisplay, 1, 0, str.tostring(shortEntry, str.tostring(f_tickFormat())), text_color = color.red)
